#  Meson Test Tertiary Beam  April 2014
#    produce at 13 deg.    Beware of stuff sneaking past collimator
#    The question of straight throughs for alignment is a question
#
#  6 inch port, 3 inch Radius = 75 mm Radius
#  H version has corner arc in, so no writing of hits for off-line reconstruction
#   show counters, possible CherenkovFki
#   Also add downstream collimator
#
#  Remember, lengths are in mm, fields is Tesla !  Momentum in MeV
#  5200 sec for 900K events  kill coll etc.
#
#  Assorted corrections made on magnet details,  8 Jan 2013
#______________________________________________________________________
#
param worldMaterial=Air
physics QGSP_BIC
param inch=25.4
 
param histoFile=sim_LAriaT_13degProdxn_10degAna_SurveyedGeom.root
## Maybe we should just let the output get dumped to the stdout.
## output $histoFile.out
#param first=0
#param last=100

param sinUS=0.224951054 #sin (13 deg)
param cosUS=0.974370065 #cos (13 deg)
param sinDS=0.052335956 #sin ( 3 deg)
param cosDS=0.998629535 #cos ( 3 deg)
 
#      killshld   used to kill tracks in shielding, significantly faster running
param killshld=0
 
#   Use Bscale to change fields.  +1 is nominal, and maximum value ( 100 A on NDBs >> 1.0)
 param Bscale=1.0
 
g4ui when=4 “/vis/scene/add/axes 1 2 3 500 mm” 
 
#    Beam used to check central momentum of spectrometer ( no target )
#beam gaussian particle=proton nEvents=10000 sigmaX=2.0 sigmaY=2.0 beamZ=00.0 \
#   meanMomentum=720.0 sigmaP=50.0 rotation=y-13

 beam gaussian particle=pi+ firstEvent=$first lastEvent=$last sigmaX=2.0 sigmaY=2.0 beamZ=-500.0 \
   meanMomentum=8000.0
	
 trackcuts  keep=pi+,pi-,pi0,kaon+,kaon-,mu+,mu-,e+,e-,gamma,proton,anti_proton

#  Cu int. length.  135 g/Cm^2;  density = 9; so int length = 3.9 cm ( 39mm )
# Target is a right parallelogramatic prism 1.25" = 31.75 mm high and wide, measured transverse to incoming beam.
# 31.75 mm / (5 tan (16 deg)) = 22.145 mm
# 31.75 mm / (5 tan (13 deg)) = 27.5048 mm (if we make a new one)
# target:
box slab height=31.75 length=209.55 width=6.35 material=Cu color=1,0.01,0.01
place slab rename=slabMinus2 x=+2*(31.75)/5 z=-2*22.145
place slab rename=slabMinus1 x=+1*(31.75)/5 z=-1*22.145
place slab rename=slabCenter  x=0*(31.75)/5  z=0*22.145
place slab rename=slabPlus2  x=-1*(31.75)/5 z=+1*22.145
place slab rename=slabPlus2  x=-2*(31.75)/5 z=+2*22.145

 
#------------------------------------ collimator -------------------------

group  NewCol
# Base plate  58 inches x 32 inches, 5.19
# Four slabs 5.19, 2, 5.19, 5.19,  all 32 inches wide
# Trap hole = 5.65*cos(16),  2.83 at ds, cos(16) at us,   42.76/cos16 long
# The Fe length = 42.76 inches ~= 1086 mm
# The Fe stack is 10 inches high =254 mm
# Collimator opening is 2" high

 box botslab height=5.19*25.4 length=58.00*25.4  width=32.0*25.4  material=Fe color=0,1,1 kill=$killshld
 box topbot  height=5.19*25.4 length=42.76*25.4  width=32.0*25.4  material=Fe color=0,1,1 kill=$killshld
 #box mid     height=2.00*25.4 length=42.76*25.4  width=32.0*25.4  material=Fe color=0,1,1 kill=$killshld

 box mid    height=2.00*25.4 length=42.76*25.4  width=11.6*25.4  material=Fe color=0,1,1 kill=$killshld
 trap Coll height=1160.0 upperWidth=70.0 lowerWidth=150.0 length=2.0*25.4 color=1,0,0 material=Air
	
 param zmov=7.62*25.4

 place botslab  z=0.00  y=-6.19*25.4
 place topbot   z=$zmov  y=-(1.0+5.19/2.0)*25.4
 place mid      z=$zmov  y=0.0 x=+296/2+67.29 rename=midleft rotation=y-14.03
 place mid      z=$zmov  y=0.0 x=-296/2-67.29 rename=midrigh rotation=y-17.97
 place topbot   z=$zmov  y=(1.0+5.19/2.0)*25.4

 place Coll z=7.62*25.4+3.0 rotation=x-90,y-16

endgroup

 place NewCol z=(29.0+7.62)*25.4    x=-8.315*25.4+40.0  rotation=y3

#--------------------possible upstream shielding block ------------------

 box   usshld  width=1000.0 length=1000.0 height=1000.0 material=Fe color=0,1,0 kill=$killshld
# place usshld  rename=c1 z=2600.0 x=250.0
# place usshld  rename=c2 z=3700.0 x=250.0


#----------------------------------------------------------------------
 param FramKill=0
group ChFram
  box FramS height=254.0 width=63.0  length=25.0 color=1,0,1 kill=$FramKill material=Al
  box FramT height=63.0  width=128.0 length=25.0 color=1,0,1 kill=$FramKill material=Al
  place FramS z=0.0 x=-95.5 
  place FramS z=0.0 x=+95.5
  place FramT z=0.0 y=-95.5
  place FramT z=0.0 y=+95.5
 endgroup
#---------------------------------------------------------------------- 
#--------------------StartLine Fake Detector ------------------
 virtualdetector StartLine width=150 height=150 length=1 material=Air color=0.9,0.9,0.7
  place StartLine rotation=z45,y-13 z=1400.0-26.4 x=-335.0+5.7




#     Time of Flight (TOF) counter
  param TOFusThick=50.8   ### 2 inches thick?
  virtualdetector TOFus width=150 height=150 length=$TOFusThick material=LUCITE color=0.05,0.05,0.93	
   place TOFus rotation=z45,y-13 z=1400.0 x=-335.0

#  Wire chambers are Fenker chambers, 128 mm sq. instrumented, Det1, 2,3,4
#  Fermilab TM-1179      2557.000     Feb 1983

  virtualdetector Det  width=128.0  height=125.0 color=0,1,0  length=25.0

  place Det rename=Det1   z=1683.59 x=-392.33  rotation=y-13
  place ChFram            z=1683.59 x=-392.33  rotation=y-13

  place Det rename=Det2   z=3195.65 x=-738.35  rotation=y-13
  place ChFram            z=3195.65 x=-738.35  rotation=y-13
  
 genericbend NDB fieldWidth=317.5 fieldHeight=142.24 fieldLength=591.0 kill=$killshld \
	ironColor=1,0,0 ironWidth=533.4 ironHeight=474.675 ironLength=466.725
	
param B1=-0.35*$Bscale
param B1z=3889.88
param B2=-0.35*$Bscale
param B2z=4567.92


#---------------------   total 10 deg bend ---------------------------------------
	
place NDB rename=NDB22 By=$B1 z=$B1z rotation=Y-13+2.5 x=-898.05
place NDB rename=NDB21 By=$B2 z=$B2z  rotation=Y-5.5 x=-993.34
  
  place Det rename=Det3 z=5183.07 x=-1019.89 rotation=y-3 
  place ChFram          z=5183.07 x=-1019.89 rotation=y-3 


#  virtualdetector MagVet  width=200.0  height=200.0 color=1,0,1  length=25.0 kill=1
#  
#  place MagVet rename=MagVet1 z=$B2z+600.0 x=+225.0-1060.0 rotation=y-3
#  place MagVet rename=MagVet2 z=$B2z+600.0 x=-225.0-1060.0 rotation=y-3

#----------Downstream aperture collimator -------------------------
#    top and bottom blocks, and wedge side blocks
# Overall: l=36" w=30" h=23"

group DScoll
  box dstb   height=8.5*$inch length=36*$inch width=30*$inch material=Fe color=0,1,1 kill=$killshld
  box dsside height=6*$inch  length=36*$inch width=11*$inch  material=Fe color=0,.8,1 kill=$killshld

  place dsside rename=dsl  z=0 x=+9*$inch
  place dsside rename=dsr  z=0 x=-9*$inch
  place dstb   rename=dst  z=0  y=+(4.25+3)*$inch 
  place dstb   rename=dsb  z=0  y=-(4.25+3)*$inch 
endgroup

place DScoll z=6188.0 x=-1077 rotation=y-3.0

#-----------------------------------------------------------------
  ## Complex group of detectors right before the TiWindow:
  place Det rename=Det4 z=6750.94 x=-1102.77 rotation=y-3
  place ChFram          z=6750.94 x=-1102.77 rotation=y-3
  
virtualdetector Halo height=380.0 width=390.0 length=10.0 material=POLYSTYRENE color=0.8,0.01,0.85
virtualdetector HaloHole radius=70.0 length=10.2 color=0,0,0 material=Vacuum
param ctrsep=1*$inch
   # Place the Halo Hole just 1 mm ahead of the Halo paddle, 
   # so it gets first dibs on the throughgoing particles we care about. ##
   place HaloHole x=-1123.24-((1+$ctrsep)*$sinDS) z=7046.51+((1+$ctrsep)*$cosDS) rotation=y-3
   place Halo     x=-1123.24-($ctrsep*$sinDS)     z=7046.51+($ctrsep*$cosDS) rotation=y-3

#   Downstream TOF Counter  
param TOFdsThick=50.8   ### 2 inches thick?
param TOFdsHeight=609.6 ### 2 feet long?
virtualdetector TOFds width=118 height=$TOFdsHeight length=$TOFdsThick material=LUCITE color=0.05,0.05,0.93
  place TOFds rename=TOFds z=$B2z+2600.0-($cosDS*$TOFdsThick*0.5+1.0)  x=-1124.0+($sinDS*$TOFdsThick*0.5+1.0) rotation=z90,y-3  

virtualdetector TiWindow radius=114.5 length=1.0 material=Air color=1,0,0 
  place TiWindow z=$B2z+2600.0 x=-1124.3  rotation=y-3

##Back this fake detector up, 2mm in z and 2mm*tan(3 deg)= 0.105mm in x
virtualdetector BigDisk radius=875 length=1.0 material=Air color=0.9,0.9,0.7 
  place BigDisk z=$B2z+2600.0+10 x=-1124.3-0.524  rotation=y-3

#   dimensions from Lariat paper  Note - not rotated with respect to secondary beam/building
 box LarDet color=.5,.5,1 height=400.0 width=470.0 length=900.0
 place LarDet z=$B2z+2600.0+1400.0 x=-1150

 
##  material LAr A=40 Z=18 density=1.40 state=l
##  box TPC height=400 width=460 length=900 material=LAr color=0.9,0.9,0.9
##  place TPC z=$B2z+$B2ztoTPCcenter rotation=y3 x=28.8

group PunchPaddles
virtualdetector Punch height=1010.0 width=310.0 length=10.0 material=POLYSTYRENE color=0.3,0.01,0.85
param offset=(310.0/2)-10.0 ## Centers of these paddles go off the beam center a little, but overlap each other.
param root2=0.707106781

  place Punch rename=PunchUL z=+5.0  x=+$offset
  place Punch rename=PunchLR z=-5.0  x=-$offset
  place Punch rename=PunchUR z=+15  y=+$offset rotation=z+90
  place Punch rename=PunchLL z=+20  y=-$offset rotation=z+90

endgroup

place PunchPaddles z=$B2z+2600.0+($cosDS*2100) x=-1124.0-($sinDS*2100) rotation=z+45,y-3
